! Morphological Transducer for Ainu

Multichar_Symbols

! Part of speech categories
%<n%>                          ! Noun
%<v%>                          ! Verb
%<adv%>                        ! Adverb
%<adj%>                        ! Adjective
%<pn%>                         ! Pronoun
%<det%>                        ! Determiner
%<cnjsub%>                     ! subordinating conjunction
%<cnjcoo%>                     ! co-ordinating conjunction
%<prt%>                        ! Particle
%<pp%>                         ! post position
%<np%>                         ! proper noun 

! Noun tags
%<abs%>                        ! Abstract qualities
%<m%>                          ! Noun male gender
%<f%>                          ! Noun female gender
%<mg%>                         ! Noun male god gender
%<mf%>                         ! Noun male or female gender
%<end%>                        ! Noun affinity tag ("=my dear x")
%<hon%>                        ! Noun honorable tag ("=my honored x")
%<px1sg%>                      ! First person singular possessive
%<px2sg%>                      ! Second person singular possessive
%<px1pl%>                      ! First person plural possessive
%<px2pl%>                      ! Second person plural possessive
%<nmlz%>                       ! Nominalization
%<rn%>                         ! relational noun
%<bn%>                         ! bound noun

! Determiner tags
%<ndet%>                       ! numeral determiner
%<ndom%>                       ! nominal demonstrative
%<adom%>                       ! anaphoric demonstrative


! Verb tags
%<pasv1%>                      ! Passive Voice chi
%<pasv2%>                      ! Passive Voice a
%<tv%>                         ! Transitive Verb
%<iv%>                         ! Intransitive Verb
%<vaux%>                       ! Auxiliary Verb
%<vx%>                         ! Verbalization
%<tvx%>                        ! Make verb transitive
%<cop%>                        ! copula
%<acaus%> 		       ! acausative
%<caus%> 		       ! causative

! Tense tags
%<pres%>                       ! Present Tense

! Aspect tags
%<perf%>                       ! Perfective Aspect

! Mood tags
%<pri%>                        ! Indicative Mood
%<infr%>                       ! Inferential Mood

! Person tags
%<p1%>                         ! First Person
%<p2%>                         ! Second Person

! Clitic tags
%<cli%>                        ! Clitic
%<top%>                        ! Topic marker

! Adjective tags
%<comp%>                       ! Comparative
%<sup%>                        ! Superlative
%<adjx%>                       ! Adjectivization

! Article tags
%<def%>                        ! Definite Article

! Number morphology
%<pl%>                         ! Plural
%<sg%>                         ! Singular

! Other symbols
%>                             ! Morpheme boundary
%<dim%>                        ! Diminutive Particle
%<int%>                        ! Intensifying Particle  
%<ev%>                         ! Evidentiality
%<exc%>                        ! Exclamation
% 	                       ! Space

! Punctuation
%<sent%>                       ! Sentence marker
%<guio%>    		       ! Hyphen
%<cm%>       		       ! Comma
%<apos%>                       ! Apostrophe
%<rquot%>                      ! Quote marker (right hand side) 
%<lquot%>                      ! Quote marker (left hand side) 
%<rpar%>                       ! Parenthetical marker (right hand side) 
%<lpar%>                       ! Parenthetical marker (left hand side) 

! Archiphonemes
%{I%}                          ! can be イ or ヒ or null

%{i%}                          ! can be i or null

%{Mm%}                         ! mu mm or null
%{Tt%}                         ! tu tt or null
%{Kk%}                         ! ku kk or null
%{t%}                          ! small tsu or null

%{w%}                          ! ウ or null (for epenthesis)
%{pe%}                         ! pe or null
%{K%}                          ! ka, ki, ku, ke, or ko

%{U%}                          ! all sorts of stuff

! Features
%[%+adjx%]                     ! adjectivalization
%[%-adjx%]                     

%[%+comp%]                     ! comparison
%[%-comp%]

%[%+sup%]                      ! superlative
%[%-sup%]  

%[%+dInd%]                     ! Indefinite determiner
%[%-dInd%]

%[%+dDef%]                     ! Definite determiner
%[%-dDef%]

%[%+tvx%]                      ! Making verb transitive
%[%-tvx%]

%[%+pl%]                       ! plurality
%[%-pl%] 

%[%+m%]                        ! male gender
%[%-m%]

%[%+f%]                        ! female gender
%[%-f%]

%[%+pasv1%]                    ! verb passification
%[%-pasv1%]

%[%+pasv2%]                    ! verb passification
%[%-pasv2%]

%[%+sg%]                       ! singular
%[%-sg%]

%[%+int%]                      ! intensifier
%[%-int%]

%[%+mg%]                       ! male (god)
%[%-mg%]

%[%+appl%]                     ! applicative
%[%-appl%]

%[%+apass%]                    ! generalized object
%[%-apass%]

%[%+refl%]                     ! reflexive
%[%-refl%]

%[%+rec%]                      ! reciprocal
%[%-rec%]

%[%+acaus%]                    ! acausative
%[%-acaus%]

%[%+pos%]                      ! possessive common noun
%[%-pos%]

%[%+pp1sg%]                    ! possessive relational noun
%[%-pp1sg%]

%[%+1ps%]                      ! 1st person possessive 
%[%-1ps%]

%[%+2p%]                       ! 2nd person
%[%-2p%]

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                       M O R P H O T A C T I C S                         !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Root

Conjunction ;
Prefix ;
Determiner ;
Auxiliary ;
PostPosition ;
Particle ;
Nominalizer ;
Exclamation ;
Punctuation ;
Pronoun ;
ProperNoun ;
Clitic ;
Adverb ;

LEXICON Prefix

%[%-pasv1%]:チ%> VerbRoot ;
%[%-tvx%]:エ%> VerbRoot ;
%[%-sup%]:イヨッタ% %> VerbRoot ;
%[%-comp%]:ナア% %> VerbRoot ;
%[%-pasv2%]:ア%> VerbRoot ;
%[%-int%]:イ%> VerbRoot ;
%[%-1ps%]:ク%> VerbPrefix-ClassI ;
%[%-2p%]%[%-pos%]:エ%> VerbPrefix-ClassI ;
NounPrefix ;
VerbRoot ;
VerbPrefix-ClassI ;

LEXICON NounPrefix

%[%-dInd%]:シネ%  NounRoot ;
%[%-dDef%]:ネイ%  NounRoot ;
%[%-pl%]:ウ%> NounRoot ;
%[%-m%]:ピンネ%  NounRoot ;
%[%-f%]:マㇳネ%  NounRoot ;
%[%-mg%]:オッカイ%  NounRoot ;
%[%-sg%]:シネ%  NounRoot ;
%[%-pos%]:ク NounRoot ;
%[%-pp1sg%]:エン NounRoot ;
NounRoot ;

LEXICON VerbPrefix-ClassI 

! applicative
%[%-appl%]:エ VerbPrefix-ClassII ;
%[%-appl%]:コ VerbPrefix-ClassII ;
%[%-appl%]:オ VerbPrefix-ClassII ;
VerbPrefix-ClassII ;

LEXICON VerbPrefix-ClassII 

%[%-apass%]:イ VerbPrefix-ClassIII ;
%[%-refl%]:ヤィ VerbPrefix-ClassIII ;
%[%-rec%]:ウ VerbPrefix-ClassIII ;
%[%-acaus%]:シ VerbPrefix-ClassIII ;
VerbPrefix-ClassIII ;

LEXICON VerbPrefix-ClassIII 

%[%-appl%]:エ VerbPrefix-ClassII ;
%[%-appl%]:コ VerbPrefix-ClassII ;
%[%-appl%]:オ VerbPrefix-ClassII ;
VerbRoot ;

LEXICON Verb-TV

%<v%>%<tv%>:  # ;
%<v%>%<tv%>:  RegVerbInfl ;
%<v%>%<tv%>%<pasv1%>%[%+pasv1%]:  # ;
%<v%>%<tv%>%<pasv2%>%[%+pasv2%]:  # ;

LEXICON Verb-IV

%<v%>%<iv%>:  # ; 
%<v%>%<iv%>:  RegVerbInfl ; 
%<v%>%<iv%>%<tvx%>%[%+tvx%]:  # ;

LEXICON RegVerbInfl

%<v%>:%> Diminutive ;
%<int%>%[%+int%]:  # ;
%<nmlz%>%<abs%>:%>% イ # ;
%<nmlz%>%<abs%>:%>% ヒ # ;
%<pasv1%>%[%+pasv1%]: NounInfl-Common ;
%<pasv1%>%[%+pasv1%]: # ;
%<pasv2%>%[%+pasv2%]: NounInfl-Common ;
%<pasv2%>%[%+pasv2%]: # ;
%<acaus%>%[%+acaus%]: # ; 
%<vaux%>%<vpres%>:シリネ # ;
%<pl%>:パ # ;
%<comp%>%[%+comp%]:  # ;
%<sup%>%[%+sup%]:  # ;
%<p1%>%<sg%>%[%+1ps%]:  # ;
%<p2%>%<pos%>%[%+2p%]%[%+pos%]:  # ;
%<caus%>:%>レ # ;

VerbSuffix-ClassVI ;

LEXICON VerbSuffix-ClassVI 

%<caus%>:%>レ # ;

LEXICON Diminutive

%<dim%>:%>ポ # ;

LEXICON NounInfl-utara
%<n%>%<pl%>:%>% ウタラ # ;
NounInfl-Common ;

LEXICON NounInfl-U
%<n%>%<pl%>:%>%{U%} # ;
NounInfl-Common ;

LEXICON NounInfl-preU

%<n%>%<pl%>%[%+pl%]: # ;
NounInfl-Common ;

LEXICON NounInfl-Nu
%<n%>%<pl%>:%>ヌ # ;
NounInfl-Common ;

LEXICON NounInfl-Irreg
%<n%>%<pl%>:  # ;
NounInfl-Common ;

LEXICON NounInfl-Common

%<n%>: Diminutive ;
%<nmlz%>:%>% ㇷ゚ # ;
%<pn%>%<px1sg%>%<mf%>:%>コロ Diminutive ;
%<n%>%<vz%>:%>コロ  # ;
%<n%>%<vz%>:%>カラ  # ;
%<n%>%<det%>%<ind%>%[%+dInd%]:  # ;
%<n%>%<det%>%<def%>%[%+dDef%]:  # ;
%<n%>%<sg%>%[%+sg%]:  # ;
%<n%>%<m%>%[%+m%]:  # ;
%<n%>%<mg%>%[%+mg%]:  # ;
%<n%>%<f%>%[%+f%]:  # ;
%<n%>%<end1%>:%>% トノケ # ;
%<n%>%<end2%>:%>% ポ # ;
%<n%>%<hon%>:%>% ニㇱパケ # ;
%<n%>%<poss%>:%>%{w%}ェ # ;
%<n%>:  # ;
%<n%>%<cn%>%<pos%>%[%+pos%]:イヒ # ;


LEXICON NounInfl-Relational

%<n%>%<rn%>%<pos%>%<p1%>%<sg%>%[%+pp1sg%]:  # ;
%<n%>%<rn%>%<pos%>%<p3%>%<sg%>:ア # ;

!LEXICON Verb-IV
!
!%<adj%>%<adv%>:%>タラ # ;
!%<adj%>%<pl%>:%>パ # ;
!%<adj%>:%> AbstractNoun ;
!%<adj%>%<comp%>%[%+comp%]:  # ;
!%<adj%>%<sup%>%[%+sup%]:  # ;
!%<v%>%<iv%>%<nmlz%>%<abs%>:%>% イ # ;
!%<v%>%<iv%>%<nmlz%>%<abs%>:%>% ヒ # ;
!%<v%>%<iv%>%<nmlz%>:%>% ㇷ゚ # ; 
!%<adj%>:  # ;

!LEXICON AbstractNoun
!%<n%>%<abs%>:ポ Diminutive ;

!LEXICON Verb-IV
!
!%<adv%>:%>ノ # ;


LEXICON Copula

%<v%>%<cop%>:  # ;

!LEXICON Verb-IV
!
!%<adj%>:%>テ Verb-IV ;


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                          L E X I C O N                                  !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON NounRoot

house:house NounInfl-Common ; ! A noun
エ:エ NounInfl-Common ; ! a noun
チ:チ NounInfl-Common ; !
エチ:エチ NounInfl-Common ; !
ク:ク NounInfl-Common ; !
イセ:イセ NounInfl-Common ; !
ポンペ:ポンペ NounInfl-Common ; !
チカㇷ゚:チカㇷ゚ NounInfl-Common ; !
オッカィ:オッカィ NounInfl-Common ; !
チイカナィ:チイカナィ NounInfl-Common ; !
チㇱナィ:チㇱナィ NounInfl-Common ; !
ト゚カラ:ト゚カラ NounInfl-Common ; !
イキリ:イキリ NounInfl-Common ; ! a seam 
アット゚ㇱ:アット゚ㇱ NounInfl-Common ; ! cloth 
シセイ:シセイ NounInfl-Common ; ! a house
チセ:チセ NounInfl-Common ; ! house
ハウ:ハウ NounInfl-Common ; ! the voice
ホニ:ホニ NounInfl-Common ; ! the stomach
カヤ:カヤ NounInfl-Common ; ! a sail
アイアイ:アイアイ NounInfl-utara ; !
アイヌ:アイヌ NounInfl-utara ; ! human/the Ainu
アシキペㇳ:アシキペㇳ NounInfl-U ; !
アㇳ:ア%{Tt%} NounInfl-U ; !
アㇺ:ア%{Mm%} NounInfl-U ; !
イクㇱペ:%{i%}クㇱペ NounInfl-preU ; !
イタㇰ:イタ%{Kk%} NounInfl-U ; !
イリワㇰ:イリワㇰ NounInfl-preU ; !
ウㇺマ:ウㇺマ NounInfl-utara ; !
クㇳ:ク%{t%} NounInfl-U ; !
ケマ:ケマ NounInfl-preU ; !
チェㇷ゚:チェㇷ゚ NounInfl-Nu ; !
ニシ:ニシ NounInfl-U ; !
ニマキ:ニマキ NounInfl-preU ; !
フラ:フラ NounInfl-Nu ; !
ペ:ペ%{pe%} NounInfl-Irreg ; !
ペㇳ:ペㇳ NounInfl-Irreg ; !
チコイキㇷ゚:チコイキㇷ゚ NounInfl-Common ; !
チセイ:チセイ NounInfl-Common ; !
シウェンテㇷ゚:シウェンテㇷ゚ NounInfl-Common ; !
クル:クル NounInfl-Common ; !
チェㇷ゚:チェㇷ゚ NounInfl-Nu ; !
クッペ:クッペ NounInfl-preU ; !
セタ:セタ NounInfl-Common ; !
アアㇰ:アアㇰ NounInfl-Common ; 
アトノ:アトノ NounInfl-Common ; 
アニㇱ:アニㇱ NounInfl-Common ; 
アポホ:アポホ NounInfl-Common ; 
アミチ:アミチ NounInfl-Common ; 
アユポ:アユポ NounInfl-Common ; 
アㇰ:アㇰ NounInfl-Common ; 
ト゚レㇱ:ト゚レㇱ NounInfl-Common ; 
シㇰ:シㇰ NounInfl-Common ;
カムィ:カムィ NounInfl-Common ;
パ:パ NounInfl-Common ;
サㇺ:サㇺ NounInfl-Relational ;
ル:ル NounInfl-Common ;
ニㇱパ:ニㇱパ NounInfl-Common ;
ポネ:ポネ NounInfl-Common ; ! bone
シタ:シタ NounInfl-Common ; ! dog
ウパㇱ:ウパㇱ NounInfl-Common ; ! snow
イㇽ:イㇽ NounInfl-Common ; ! family/kin
フチ:フチ NounInfl-Common ; ! grandmother/old woman
フㇺイ:フㇺイ NounInfl-Common ; ! sound (possessive)
アペ:アペ NounInfl-Common ; ! fire
ワッカ:ワッカ NounInfl-Common ; ! water
ケㇺ:ケㇺ NounInfl-Common ; ! blood
アト゚ィ:アト゚ィ NounInfl-Common ; ! sea/ocean
アマㇺ:アマㇺ NounInfl-Common ; ! meal (lit. rice/cereals)
アㇷ゚ト:アㇷ゚ト NounInfl-Common ; ! rain
マチヤ:マチヤ NounInfl-Common ; ! town
ヌㇷ゚:ヌㇷ゚ NounInfl-Common ; ! field
オㇱケ:オㇱケ NounInfl-Common ; ! inside (noun locative)
ヌプリ:ヌプリ NounInfl-Common ; ! mountain
モンラィケ:モンラィケ NounInfl-Common ; ! work
ケラ:ケラ NounInfl-Common ; ! taste
パロ:パロ NounInfl-Common ; ! mouth (possessive)
イタピルㇷ゚:イタピルㇷ゚ NounInfl-Common ; ! cloth
カヤ:カヤ NounInfl-Common ; ! sail
チュㇰ:チュㇰ NounInfl-Common ; ! autumn
クㇽ:クㇽ NounInfl-Common ; ! person
ポト゚ㇽケㇱ:ポト゚ㇽケㇱ NounInfl-Common ; ! bruise
オヤシㇺ:オヤシㇺ  NounInfl-Common ; ! day after tomorrow
オヤシㇺシㇺケ:オヤシㇺシㇺケ  NounInfl-Common ; ! 2 days after tomorrow
ケマ:ケマ NounInfl-Common ; ! leg
サパ:サパ NounInfl-Common ; ! head
シケ:シケ NounInfl-Common ; ! luggage
プィ:プィ NounInfl-Common ; ! hole
スㇺ:スㇺ NounInfl-Common ; ! oil
イト゚ンナㇷ゚:イト゚ンナㇷ゚ NounInfl-Common ; ! ant
カィテ:カィテ NounInfl-Common ; ! anchor
サポ:サポ NounInfl-Common ; ! older sister
チㇷ゚:チㇷ゚ NounInfl-Common ; ! ship
ト゚ィ:ト゚ィ NounInfl-Common ; ! stomach
チュㇷ゚:チュㇷ゚ NounInfl-Common ; ! sun/moon/month
ニペㇰ:ニペㇰ NounInfl-Common ; ! light (moon)


LEXICON Clitic

アナㇰ%<cli%>%<top%>:アナㇰ # ;

LEXICON Pronoun

アオカィ%<n%>%<pn%>:アオカィ # ;

LEXICON VerbRoot

モィレ:モィレ Verb-IV ; ! be late
ラチ:ラチ Verb-IV ; ! be gentle (old)
ハゥケ:ハゥケ Verb-IV ; ! be calm/cheap
ソン:ソン Verb-IV ; ! be true (old) 
ハロ:ハロ Verb-IV ;
ラム:ラム Verb-IV ;
ヌチャㇰ:ヌチャㇰ Verb-IV ;
アㇱカン:アㇱカン Verb-IV ;
ケㇺ:ケㇺ Verb-TV ; ! lick
ヌペキ:ヌペキ Verb-IV ; ! bright
パセ:パセ Verb-IV ; ! heavy
ポロ:ポロ Verb-IV ; ! large
キ:キ Verb-IV ; ! do (auxiliary)
アマ:アマ Verb-TV ; ! put/place
クパ:クパ Verb-TV ; ! bite
ペレパ:ペレパ Verb-TV ; ! cleave/split/break (old)
テレケ:テレケ Verb-IV ; ! jump
イセ:イセ Verb-IV ; ! squeak/sqeal (old)
ミㇰ:ミㇰ Verb-IV ; ! bark
ミナ:ミナ Verb-IV ; ! laugh
キラ:キラ Verb-IV ; ! escape
イタㇰ:イタㇰ Verb-IV ; ! speak
エ:エ Verb-TV ; ! eat
イェ:イェ Verb-TV ; ! say/tell
ヌ:ヌ Verb-TV ; ! hear
ヌイェ:ヌイェ Verb-TV ; ! carve/write/tattoo
ライケ:ライケ Verb-TV ; ! kill
セ:セ Verb-IV ; ! squeak/squeal
ルㇲカ:ルㇲカ Verb-IV ; ! be angry with something
ネ:ネ Copula ; ! copula
ピㇼカ:ピㇼカ Verb-IV ; ! be good
ウェン:ウェン  Verb-IV ; ! be bad/poor
ハロコロ:ハロコロ Verb-IV ; ! be fat (old)
ポン:ポン Verb-IV ; ! be small (old)
アン:アン Verb-IV ; ! exist/live
ハウェアン:ハウェアン Verb-IV ; ! say
シノㇳ:シノㇳ Verb-IV ; ! play (have fun)
ル:ル Verb-IV ; ! melt
エセ:エセ Verb-TV ; ! answer
キ:キ Verb-TV ; ! do (tv)
アㇽパ:アㇽパ Verb-IV ; ! go (iv)
エㇰ:エㇰ Verb-IV ; ! come (iv)
ラィ:ラィ Verb-IV ; ! die (iv)
ペゥレ:ペゥレ Verb-IV ; ! be young
ヌカㇽ:ヌカㇽ Verb-TV ; ! see
モンラィケ:モンラィケ Verb-IV ; ! do work 
イッカ:イッカ Verb-IV ; ! steal
チㇱ:チㇱ Verb-IV ; ! cry
モコㇽ:モコㇽ Verb-IV ; ! sleep
アㇱ:アㇱ Verb-IV ; ! stand
アㇷ゚カㇱ:アㇷ゚カㇱ Verb-IV ; ! walk
ヘト゚ク:ヘト゚ク Verb-IV ; ! come out/be born
アフン:アフン Verb-IV ; ! enter (sg) TODO: figure out how to deal with this
カリ:カリ Verb-IV ; ! spin
ニン:ニン Verb-IV ; ! decrease
チ:チ Verb-TV ; ! get burnt/dry/boiled

LEXICON Adverb

エネ%<adv%>:エネ # ; ! thus/so/in such a way
ソモ%<adv%>:ソモ # ; ! negation
イテキ%<adv%>:イテキ # ; ! negative command
オロタ%<adv%>:オロタ # ; ! in which

LEXICON PostPosition

タ%<pp%>:タ # ; ! at
パㇰノ%<pp%>:パㇰノ # ; ! until
アニ%<pp%>:アニ # ; ! final particle/
ピㇱノ%<pp%>:ピㇱノ # ; ! every TODO: fix no-suffix 

LEXICON Auxiliary

ア%<perf%>%<sg%>:ア # ; 
ナンコㇿ%<v%>%<vaux%>:ナンコㇿ # ; ! TODO: fix this logic

LEXICON Conjunction

コ%<cnjsub%>:コ # ;
クス%<cnjsub%>:クス # ;
ワ%<cnjcoo%>:ワ # ; ! and
オロワ%<cnjcoo%>:オロワ # ;


LEXICON Determiner

ト゚ㇷ゚%<bn%>%<ndet%>:ト゚ㇷ゚ # ;

LEXICON Nominalizer

ヒ%<nmlz%>:ヒ # ;
イ%<nmlz%>:イ # ;
ルウェ%<nmlz%>%<ev%>%<infr%>:ルウェ # ;

LEXICON Particle 

カ%<prt%>%<adv%>:カ # ;

LEXICON Exclamation

アニ%<exc%>:アニ # ;

LEXICON ProperNoun

アㇱカンネ% ラマㇳ%<np%>:アㇱカンネ% ラマㇳ # ;
ヨロタン%<np%>:ヨロタン # ; ! jordan
イェス%<np%>:イェス # ; ! jesus


LEXICON Punctuation

.%<sent%>:. # ;
..%<sent%>:.. # ;
...%<sent%>:... # ;
%;%<sent%>:%; # ;
%:%<sent%>:%: # ;
%!%<sent%>:%! # ;
%-%<guio%>:%- # ;
%—%<guio%>:%— # ;
,%<cm%>:, # ;
%?%<sent%>:%? # ;
%'%<apos%>:%' # ;
%"%<sent%>:%" # ;
%«%<lquot%>:%« # ;
%»%<rquot%>:%» # ;
%”%<rquot%>:%” # ;
%“%<lquot%>:%“ # ;
%(%<lpar%>:%( # ;
%]%<rpar%>:%] # ;
%[%<lpar%>:%[ # ;
%)%<rpar%>:%) # ;
\%<sent%>:\ # ;
/%<sent%>:/ # ;

! Resources:
! http://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Lexicon
! http://wiki.apertium.org/wiki/Lttoolbox_and_lexc
! http://wiki.apertium.org/wiki/Apertium-specific_conventions_for_lexc
! https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
! http://hfst.sourceforge.net/
! http://www.cis.upenn.edu/~cis639/docs/lexc.html
! http://wiki.apertium.org/wiki/Hfst

